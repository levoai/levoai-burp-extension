buildscript {
    ext{
        odcPluginVersion = project.hasProperty('odcGradlePluginVersion') ? project.getProperty('odcGradlePluginVersion') : '6.3.1'
    }
    repositories {
        mavenCentral()
    }
}

apply plugin: 'java'
apply plugin: 'maven-publish'

group 'ai.levo'
version '0.2.1'

def burpExtensionHomepage = 'https://github.com/levoai/levoai-burp-extension'
def burpExtensionJarName = 'LevoAiBurpExtension.jar'

repositories {
    mavenCentral()
}

dependencies {
    implementation('net.portswigger.burp.extender:burp-extender-api:2.3')
    implementation('com.fasterxml.jackson.core:jackson-databind:2.21.0')
}

compileJava {
    targetCompatibility '11'
    sourceCompatibility '11'
}

task fatJar(type: Jar) {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes("Implementation-Version": project.version, "Implementation-Title": project.name, "Implementation-URL": burpExtensionHomepage)
    }
    archiveFileName = burpExtensionJarName
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

task addVersionToProperties(dependsOn: processResources) {
    doLast {
        def props = new Properties()
        file("$buildDir/resources/main/settings.properties").withInputStream { props.load(it) }
        props["version"] = project.version.toString()
        file("$buildDir/resources/main/settings.properties").withOutputStream { props.store(it, null) }
    }
}

classes {
    dependsOn addVersionToProperties
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/levoai/levoai-burp-extension")
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            artifact fatJar
        }
    }
}
